!============================================================================================================ xX =================
!        _____     _____    _______________    _______________   _______________            .xXXXXXXXx.       X
!       /    /)   /    /)  /    _____     /)  /    _____     /) /    _____     /)        .XXXXXXXXXXXXXXx  .XXXXx
!      /    //   /    //  /    /)___/    //  /    /)___/    // /    /)___/    //       .XXXXXXXXXXXXXXXXXXXXXXXXXx
!     /    //___/    //  /    //   /    //  /    //___/    // /    //___/    //      .XXXXXXXXXXXXXXXXXXXXXXX`
!    /    _____     //  /    //   /    //  /    __________// /    __      __//      .XX``XXXXXXXXXXXXXXXXX`
!   /    /)___/    //  /    //   /    //  /    /)_________) /    /)_|    |__)      XX`   `XXXXX`     .X`
!  /    //   /    //  /    //___/    //  /    //           /    //  |    |_       XX      XXX`      .`
! /____//   /____//  /______________//  /____//           /____//   |_____/)    ,X`      XXX`
! )____)    )____)   )______________)   )____)            )____)    )_____)   ,xX`     .XX`
!                                                                           xxX`      XXx
! Copyright (C) 2015  Prof. Claus-Dieter Munz <munz@iag.uni-stuttgart.de>
! This file is part of HOPR, a software for the generation of high-order meshes.
!
! HOPR is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License 
! as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
!
! HOPR is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty
! of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License for more details.
!
! You should have received a copy of the GNU General Public License along with HOPR. If not, see <http://www.gnu.org/licenses/>.
!=================================================================================================================================
MODULE MOD_PsiEval
!===================================================================================================================================
! ?
!===================================================================================================================================
! MODULES
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
PUBLIC
!-----------------------------------------------------------------------------------------------------------------------------------
! GLOBAL VARIABLES 
!-----------------------------------------------------------------------------------------------------------------------------------
! Private Part ---------------------------------------------------------------------------------------------------------------------
! Public Part ----------------------------------------------------------------------------------------------------------------------
!===================================================================================================================================

CONTAINS


FUNCTION EvalPsi(x,y)
!===================================================================================================================================
! Evaluate Psi functions
!===================================================================================================================================
! MODULES
USE MOD_Solov_Vars,ONLY:psiCoefs
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL, INTENT(IN) :: x
REAL, INTENT(IN) :: y
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL      :: EvalPsi
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
!===================================================================================================================================
EvalPsi=SUM(EvalPsiVec(x,y)*PsiCoefs)

END FUNCTION EvalPsi


FUNCTION EvaldPsi(dir,nn,x,y)
!===================================================================================================================================
! Evaluate Psi functions
!===================================================================================================================================
! MODULES
USE MOD_Solov_Vars,ONLY:psiCoefs
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
INTEGER,INTENT(IN) :: dir !1:x 2:y
INTEGER,INTENT(IN) :: nn !nth derivative
REAL, INTENT(IN)   :: x
REAL, INTENT(IN) :: y
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL      :: EvaldPsi
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
!===================================================================================================================================
SELECT CASE(dir)
CASE(1)
  SELECT CASE(nn)
  CASE(1)
    EvaldPsi=SUM(EvaldPsidxVec(x,y)*PsiCoefs)
  CASE(2)
    EvaldPsi=SUM(Evald2PsidxVec(x,y)*PsiCoefs)
  END SELECT
CASE(2)
  SELECT CASE(nn)
  CASE(1)
    EvaldPsi=SUM(EvaldPsidyVec(x,y)*PsiCoefs)
  CASE(2)
    EvaldPsi=SUM(Evald2PsidyVec(x,y)*PsiCoefs)
  END SELECT
CASE(3) !mixed d^2/(dxdy)
  EvaldPsi=SUM(Evald2PsidxdyVec(x,y)*PsiCoefs)
END SELECT

END FUNCTION EvaldPsi


FUNCTION EvalPsiVec(x,y)
!===================================================================================================================================
! Evaluate Psi functions
!===================================================================================================================================
! MODULES
USE MOD_Solov_Vars,ONLY:p_A
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL, INTENT(IN) :: x
REAL, INTENT(IN) :: y
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL      :: EvalPsiVec(0:7)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
REAL      :: x2,y2,lgx
!===================================================================================================================================
x2=x*x
y2=y*y
lgx=LOG(x)
EvalPsiVec(0) = 0.125*x2*(x2 + p_A * (4. * lgx - x2 ))
EvalPsiVec(1) = 1.
EvalPsiVec(2) = x2
EvalPsiVec(3) = y2 - x2 * lgx
EvalPsiVec(4) = x2*(x2 - 4. * y2)
EvalPsiVec(5) = y2*( 2. * y2 - 9. * x2)   + 3.*lgx*x2*( x2 - 4. * y2 )
EvalPsiVec(6) = x2*(x2*(x2 - 12. * y2)) + 8.*x2*y2*y2
EvalPsiVec(7) = y2*(y2*(8.*y2 - (140.+120.*lgx)* x2 )) + x2*( (75 +180.*lgx)*x2*y2  - 15.*x2*x2*lgx)
         
END FUNCTION EvalPsiVec


FUNCTION EvaldPsidxVec(x,y)
!===================================================================================================================================
! Evaluate d/dx (Psi) functions
!===================================================================================================================================
! MODULES
USE MOD_Solov_Vars,ONLY:p_A
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL, INTENT(IN) :: x
REAL, INTENT(IN) :: y
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL      :: EvaldPsidxVec(0:7)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
REAL      :: x2,y2,lgx
!===================================================================================================================================
x2=x*x
y2=y*y
lgx=LOG(x)
EvaldPsidxVec(0) = x*(p_A*(1.*lgx + 0.5) + 0.5*x2*(1-p_A))
EvaldPsidxVec(1) = 0.
EvaldPsidxVec(2) = 2.*x
EvaldPsidxVec(3) = -x*(2.*lgx + 1)
EvaldPsidxVec(4) = x*4.*(x2 - 2.*y2)
EvaldPsidxVec(5) = x*(x2*(12.*lgx + 3.) - y2*(24.*lgx + 30.))
EvaldPsidxVec(6) = x*(x2*6.*(x2 - 8.*y2) + 16.*y2*y2)
EvaldPsidxVec(7) = x*(x2*(y2*(720.*lgx + 480.)- x2*(90.*lgx + 15.) ) - y2*y2*(240.*lgx + 400.)) 
         
END FUNCTION EvaldPsidxVec


FUNCTION Evald2PsidxVec(x,y)
!===================================================================================================================================
! Evaluate d^2/dx^2 (Psi) functions
!===================================================================================================================================
! MODULES
USE MOD_Solov_Vars,ONLY:p_A
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL, INTENT(IN) :: x
REAL, INTENT(IN) :: y
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL      :: Evald2PsidxVec(0:7)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
REAL      :: x2,y2,lgx
!===================================================================================================================================
x2=x*x
y2=y*y
lgx=LOG(x)
Evald2PsidxVec(0) = p_A*(lgx + 1.5) + 1.5*x2*(1.-p_A)
Evald2PsidxVec(1) = 0.
Evald2PsidxVec(2) = 2
Evald2PsidxVec(3) = -(2.*lgx + 3.)
Evald2PsidxVec(4) = 12.*x2 - 8.*y2
Evald2PsidxVec(5) = x2*(36.*lgx + 21.) - y2*(24.*lgx + 54.)
Evald2PsidxVec(6) = x2*(30.*x2 - 144.*y2) + 16*y2*y2
Evald2PsidxVec(7) = x2*(y2*(2160*lgx + 2160) - x2*(450.*lgx + 165.) ) - y2*y2*(240.*lgx + 640.) 
         
END FUNCTION Evald2PsidxVec


FUNCTION Evald2PsidxdyVec(x,y)
!===================================================================================================================================
! Evaluate d^2/(dxdy) (Psi) functions
!===================================================================================================================================
! MODULES
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL, INTENT(IN) :: x
REAL, INTENT(IN) :: y
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL      :: Evald2PsidxdyVec(0:7)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
REAL      :: x2,y2,lgx
!===================================================================================================================================
x2=x*x
y2=y*y
lgx=LOG(x)
Evald2PsidxdyVec(0) = 0.
Evald2PsidxdyVec(1) = 0.
Evald2PsidxdyVec(2) = 0.
Evald2PsidxdyVec(3) = 0.
Evald2PsidxdyVec(4) = -16.*x*y
Evald2PsidxdyVec(5) = x*y*(-48.*lgx - 60.)
Evald2PsidxdyVec(6) = x*y*(-96.*x2 + 64.*y2)
Evald2PsidxdyVec(7) = x*y*(x2*(1440.*lgx + 960.) + y2*(-960.*lgx - 1600.))
END FUNCTION Evald2PsidxdyVec


FUNCTION EvaldPsidyVec(x,y)
!===================================================================================================================================
! Evaluate d/dy (Psi) functions
!===================================================================================================================================
! MODULES
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL, INTENT(IN) :: x
REAL, INTENT(IN) :: y
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL      :: EvaldPsidyVec(0:7)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
REAL      :: x2,y2,lgx
!===================================================================================================================================
x2=x*x
y2=y*y
lgx=LOG(x)
EvaldPsidyVec(0) = 0. 
EvaldPsidyVec(1) = 0.
EvaldPsidyVec(2) = 0.
EvaldPsidyVec(3) = 2*y
EvaldPsidyVec(4) = -8.*x2*y
EvaldPsidyVec(5) =  8.*y*y2 -x2*y*(24.*lgx + 18.)
EvaldPsidyVec(6) = x2*(-24.*x2*y + 32.*y*y2)
EvaldPsidyVec(7) = x2*(x2*y*(360.*lgx + 150.)) + y2*y*(x2*(-480.*lgx - 560.) + 48*y2)
         
END FUNCTION EvaldPsidyVec


FUNCTION Evald2PsidyVec(x,y)
!===================================================================================================================================
! Evaluate d/dy (Psi) functions
!===================================================================================================================================
! MODULES
! IMPLICIT VARIABLE HANDLING
IMPLICIT NONE
!-----------------------------------------------------------------------------------------------------------------------------------
! INPUT VARIABLES
REAL, INTENT(IN) :: x
REAL, INTENT(IN) :: y
!-----------------------------------------------------------------------------------------------------------------------------------
! OUTPUT VARIABLES
REAL      :: Evald2PsidyVec(0:7)
!-----------------------------------------------------------------------------------------------------------------------------------
! LOCAL VARIABLES
REAL      :: x2,y2,lgx
!===================================================================================================================================
x2=x*x
y2=y*y
lgx=LOG(x)
Evald2PsidyVec(0) = 0. 
Evald2PsidyVec(1) = 0.
Evald2PsidyVec(2) = 0.
Evald2PsidyVec(3) = 2.
Evald2PsidyVec(4) = -8.*x2
Evald2PsidyVec(5) =  24.*y2 -x2*(24.*lgx + 18.)
Evald2PsidyVec(6) = x2*(-24.*x2 + 96.*y2)
Evald2PsidyVec(7) = x2*(x2*(360.*lgx + 150.)) + y2*(x2*(-1440.*lgx - 1680.) + 240.*y2)

END FUNCTION Evald2PsidyVec

END MODULE MOD_PsiEval
